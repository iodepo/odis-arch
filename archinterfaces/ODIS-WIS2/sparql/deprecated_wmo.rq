PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX schema: <https://schema.org/>
PREFIX schemawrong: <http://schema.org/>

SELECT DISTINCT ?s ?temporal ?identifier ?keywords ?name ?geom ?description
WHERE
{
    ?s rdf:type ?type
    FILTER ( ?type IN (schema:Dataset, schemawrong:Dataset  ) )
    ?s schema:name | schemawrong:name ?name .
    ?s schema:description | schemawrong:description ?description .
    ?s schema:spatialCoverage ?sc .
    ?s schema:temporalCoverage ?temporal .
    ?s schema:identifier ?identifier .
    ?sc a  schema:Place .
    ?sc schema:geo ?geo .
    ?geo a ?geotype .
    ?geo ?geompred ?geom .
    FILTER(!isIRI(?geom))
    OPTIONAL {?s schema:keywords | schemawrong:keywords ?keywords}
}
LIMIT 100